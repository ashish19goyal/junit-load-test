apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'io.github.ashish19goyal'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    implementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    implementation 'org.junit.platform:junit-platform-launcher:1.5.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    implementation 'org.apache.commons:commons-math3:3.0'
}

// used for publishing this library to maven central
// As described here
// https://central.sonatype.org/publish/publish-gradle/
task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'junit-load-test'
            from components.java

            pom {
                name = 'Junit Load Test'
                description = 'This library provides a junit test engine to perform load testing'
                url = 'https://github.com/ashish19goyal/junit-load-test'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ashish18goyal'
                        name = 'Ashish Goyal'
                        email = 'ashish.19goyal@gmail.com'
                    }
                }
                scm {
                    url 'scm:git@github.com/ashish19goyal/junit-load-test.git'
                    connection 'scm:git@github.com/ashish19goyal/junit-load-test.git'
                    developerConnection 'scm:git@github.com/ashish19goyal/ashish19goyal.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
